apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    buildToolsVersion rootProject.buildToolsVersion
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionName rootProject.versionName
        versionCode rootProject.versionCode

        resValue "integer", "xikolo_api_version", rootProject.xikoloApiVersion.toString()
        resValue "integer", "realm_schema_version", rootProject.realmSchemaVersion.toString()
    }

    flavorDimensions "brand", "build"

    productFlavors.all {
        ext.appName = null
        ext.appHost = null
    }

    productFlavors {
        regular {
            dimension "build"
        }
        fast {
            // faster dev builds
            minSdkVersion 21
            dimension "build"
        }

        openHPI {
            applicationId "de.xikolo.openhpi"
            buildConfigField "de.xikolo.config.BuildFlavor", "X_FLAVOR", "de.xikolo.config.BuildFlavor.OPEN_HPI"
            appName = "openHPI"
            appHost = "open.hpi.de"
            dimension "brand"
        }
        openHPI_CN {
            applicationId "de.xikolo.openhpi.cn"
            buildConfigField "de.xikolo.config.BuildFlavor", "X_FLAVOR", "de.xikolo.config.BuildFlavor.OPEN_HPI_CN"
            appName = "openHPI CN"
            appHost = "openhpi.cn"
            dimension "brand"
        }
        openSAP {
            applicationId "de.xikolo.opensap"
            buildConfigField "de.xikolo.config.BuildFlavor", "X_FLAVOR", "de.xikolo.config.BuildFlavor.OPEN_SAP"
            appName = "openSAP"
            appHost = "open.sap.com"
            dimension "brand"
        }
        moocHouse {
            applicationId "de.xikolo.moochouse"
            buildConfigField "de.xikolo.config.BuildFlavor", "X_FLAVOR", "de.xikolo.config.BuildFlavor.MOOC_HOUSE"
            appName = "mooc.house"
            appHost = "mooc.house"
            dimension "brand"
        }
        moocHouse_CN {
            applicationId "de.xikolo.moochouse.cn"
            buildConfigField "de.xikolo.config.BuildFlavor", "X_FLAVOR", "de.xikolo.config.BuildFlavor.MOOC_HOUSE_CN"
            appName = "cnMOOC.house"
            appHost = "cnmooc.house"
            dimension "brand"
        }
        openUNE {
            applicationId "de.xikolo.openune"
            buildConfigField "de.xikolo.config.BuildFlavor", "X_FLAVOR", "de.xikolo.config.BuildFlavor.OPEN_UNE"
            appName = "openUNE"
            appHost = "openune.cn"
            dimension "brand"
        }
        openWHO {
            applicationId "de.xikolo.openwho"
            buildConfigField "de.xikolo.config.BuildFlavor", "X_FLAVOR", "de.xikolo.config.BuildFlavor.OPEN_WHO"
            appName = "OpenWHO"
            appHost = "openwho.org"
            dimension "brand"
        }
    }
    signingConfigs {
        if (rootProject.file('signing.properties').exists()) {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file('signing.properties')))
            openHPI {
                storeFile rootProject.file(props.RELEASE_STORE_FILE)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS_OPENHPI
                keyPassword props.RELEASE_KEY_PASSWORD_OPENHPI
            }
            openHPI_CN {
                storeFile rootProject.file(props.RELEASE_STORE_FILE)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS_OPENHPI_CN
                keyPassword props.RELEASE_KEY_PASSWORD_OPENHPI_CN
            }
            openSAP {
                storeFile rootProject.file(props.RELEASE_STORE_FILE)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS_OPENSAP
                keyPassword props.RELEASE_KEY_PASSWORD_OPENSAP
            }
            moocHouse {
                storeFile rootProject.file(props.RELEASE_STORE_FILE)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS_MOOCHOUSE
                keyPassword props.RELEASE_KEY_PASSWORD_MOOCHOUSE
            }
            moocHouse_CN {
                storeFile rootProject.file(props.RELEASE_STORE_FILE)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS_MOOCHOUSE_CN
                keyPassword props.RELEASE_KEY_PASSWORD_MOOCHOUSE_CN
            }
            openUNE {
                storeFile rootProject.file(props.RELEASE_STORE_FILE)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS_OPENUNE
                keyPassword props.RELEASE_KEY_PASSWORD_OPENUNE
            }
            openWHO {
                storeFile rootProject.file(props.RELEASE_STORE_FILE)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS_OPENWHO
                keyPassword props.RELEASE_KEY_PASSWORD_OPENWHO
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            debuggable false
            buildConfigField "de.xikolo.config.BuildType", "X_TYPE", "de.xikolo.config.BuildType.RELEASE"

            if (rootProject.file('signing.properties').exists()) {
                productFlavors.openHPI.signingConfig signingConfigs.openHPI
                productFlavors.openHPI_CN.signingConfig signingConfigs.openHPI_CN
                productFlavors.openSAP.signingConfig signingConfigs.openSAP
                productFlavors.moocHouse.signingConfig signingConfigs.moocHouse
                productFlavors.moocHouse_CN.signingConfig signingConfigs.moocHouse_CN
                productFlavors.openUNE.signingConfig signingConfigs.openUNE
                productFlavors.openWHO.signingConfig signingConfigs.openWHO
            }
        }
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            debuggable true
            buildConfigField "de.xikolo.config.BuildType", "X_TYPE", "de.xikolo.config.BuildType.DEBUG"

            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
    applicationVariants.all { variant ->
        def name = variant.productFlavors.get(0).ext.appName
        def host = variant.productFlavors.get(0).ext.appHost
        if (variant.buildType.name == "debug") {
            name += ".debug"
        }
        variant.resValue "string", "app_name", name
        variant.resValue "string", "app_host", host
        variant.resValue "string", "service_download", name + " Download Service"
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name == "release" && names.contains("fast")) {
            setIgnore(true)
        }
    }
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        disable 'UnusedAttribute'
    }
    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply from: 'dependencies.gradle'
